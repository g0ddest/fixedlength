buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        if (JavaVersion.current() >= JavaVersion.VERSION_11) {
            classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:5.1.0.4882"
        }
    }
}

plugins {
    id 'maven-publish'
    id 'java'
    id 'checkstyle'
    id 'me.champeau.jmh' version '0.6.6'
    id 'signing'
    id 'jacoco'
}

if (JavaVersion.current() >= JavaVersion.VERSION_11) {
    apply plugin: "org.sonarqube"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

group 'name.velikodniy.vitaliy'
version '0.14'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.3'
    testImplementation 'org.hamcrest:java-hamcrest:2.0.0.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar, sourcesJar
}

checkstyle {
    sourceSets = [ project.sourceSets.main ]
    checkstyleMain.exclude '**/jmh/**'
}


// Github publishing
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/g0ddest/fixedlength")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'fixedlength'
                packaging = 'jar'
                description = 'Fast simple zero-dependency Java library to parse fixed length files'
                url = 'https://github.com/g0ddest/fixedlength'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://github.com/g0ddest/fixedlength/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'g0ddest'
                        name = 'Vitalii VelicodnÃ®i'
                        email = 'vitaliy@velikodniy.name'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/g0ddest/fixedlength.git'
                    developerConnection = 'scm:git:ssh://git@github.com:g0ddest/fixedlength.git'
                    url = 'https://github.com/g0ddest/fixedlength.git'
                }
            }
        }
    }
}

signing {
    def signingKey = System.getenv('SIGNING_KEY')
    def signingPassword = System.getenv('SIGNING_PASSWORD')
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

if (JavaVersion.current() >= JavaVersion.VERSION_11) {
    sonar {
        properties {
            property "sonar.projectKey", "g0ddest_fixedlength"
            property "sonar.organization", "g0ddest"
            property "sonar.host.url", "https://sonarcloud.io"
            property "sonar.coverage.jacoco.xmlReportPaths", "${layout.buildDirectory.get()}/reports/jacoco/test/jacocoTestReport.xml"
        }
    }
}
